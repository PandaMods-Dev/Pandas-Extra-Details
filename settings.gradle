pluginManagement {
	repositories {
		maven { url "https://maven.fabricmc.net/" }
		maven { url "https://maven.minecraftforge.net/" }
		maven { url "https://maven.architectury.dev/" }
		gradlePluginPortal()
	}
}

def loadProperties() {
	String defaultVersion = "1.21"

	List<String> availableVersions = fileTree("versionProperties").files.name
	for (int i = 0; i < availableVersions.size(); i++) {
		availableVersions[i] = availableVersions[i].replaceAll("\\.properties", "")
	}

	availableVersions.sort()
	println "Avalible Minecraft versions: ${availableVersions}"

	String selectedVersion = ""
	int versionIndex = -1
	println "Avalible MC versions: ${availableVersions}"
	if (hasProperty("game_version")) {
		selectedVersion = game_version
		versionIndex = availableVersions.indexOf(game_version)
	}
	if (versionIndex == -1) {
		println "No 'game_version' set or the set 'game_version' is invalid! Defaulting to ${defaultVersion}."
		selectedVersion = defaultVersion
		versionIndex = availableVersions.indexOf(defaultVersion)
		assert versionIndex != -1
	}

	println "Loading properties file at " + selectedVersion + ".properties"
	var properties = new Properties()
	properties.load(new FileInputStream("${rootDir}/versionProperties/${selectedVersion}.properties"))

	properties.each { prop ->
		gradle.ext.set(prop.key, prop.value)
	}
	gradle.ext.availableVersions = availableVersions
	gradle.ext.versionIndex = versionIndex
}
loadProperties()

rootProject.name = mod_name

include("common")
for (loader in ((String) gradle.supported_modloaders).split(",")) {
	def loaderName = loader.strip()
	println "Adding loader " + loaderName
	include(loaderName)
}

